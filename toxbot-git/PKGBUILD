# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=toxbot
pkgname=toxbot-git
pkgver=r38.3dd3955
pkgrel=2
pkgdesc='Remotely controlled Tox bot whose purpose is to auto-invite friends to Tox groupchats'
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url='https://github.com/JFreegman/ToxBot'
license=('GPL3')
depends=('tox-git')
makedepends=('git')
conflicts=("$_pkgname")
provides=("$_pkgname")

source=("$pkgname::git+$url")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  install -Dm755 $_pkgname "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$_pkgname/README.md"
  install -Dm644 commands.txt "$pkgdir/usr/share/doc/$_pkgname/commands.txt"
}
