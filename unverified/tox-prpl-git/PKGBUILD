# Maintainer: naxuroqa <naxuroqa@gmail.com> 

pkgname=tox-prpl-git
_gitname=tox-prpl
pkgver=0.3.0.r5.g191cdcc
pkgrel=1
pkgdesc="Tox protocol plugin for pidgin / libpurple"
arch=('i686' 'x86_64')
url="http://tox.dhs.org/"
license=('GPL3')
depends=('libpurple' 'tox-git')
makedepends=('git' 'libconfig')
optdepends=('pidgin')
options=('!libtool')
conflicts=('tox-prpl')
provides=('tox-prpl')
source=('git://github.com/jin-eld/tox-prpl.git')
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git describe --long --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$_gitname"
  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd "$_gitname"
  make DESTDIR="$pkgdir" install
}
