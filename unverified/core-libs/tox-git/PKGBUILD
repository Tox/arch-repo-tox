# Maintainer: Kevin MacMartin <prurigro at gmail dot com>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: naxuroqa <naxuroqa at gmail.com>

_pkgname=tox
pkgname=$_pkgname-git
pkgver=r3156.4ad7649
pkgrel=1
pkgdesc='Secure, configuration-free, P2P Skype replacement backend'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://tox.im'
license=('GPL3')
depends=(
  'libsodium'
  'opus'
  'libvpx'
  'libconfig'
  'openal-soft-tox'
)
makedepends=('git' 'check')
optdepends=('qtox-git: qt-based frontend'
            'utox-git: lightweight tox client'
            'toxic-git: ncurses-based frontend')
conflicts=("$_pkgname")
provides=("$_pkgname")
replaces=("$_pkgname")
source=("$_pkgname::git+https://github.com/irungentoo/toxcore.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-daemon \
    --disable-ntox \
    --disable-tests
  make
}

check() {
  cd $_pkgname
  make check
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 ./other/bootstrap_daemon/tox-bootstrapd.service "$pkgdir/usr/lib/systemd/system/tox-bootstrapd.service"
}
