# Maintainer: ?

_pkgname=utox
pkgname="${_pkgname}-git"
pkgver=r1116.gf464141
pkgrel=1

pkgdesc='Lightweight Tox client'
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url='https://github.com/notsecure/uTox'
license=('GPL3')

depends=(
  'desktop-file-utils'
  'fontconfig'
  'filter_audio-git'
  'hicolor-icon-theme'
  'libdbus'
  'libxext'
  'libxrender'
  'openal'
  'tox-git'
  'v4l-utils'
)
makedepends=('git')
optdepends=('gtk2: GTK file picker')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

source=("${pkgname}::git+https://github.com/notsecure/uTox")
md5sums=('SKIP')

install="${pkgname}.install"

pkgver()
{
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build()
{
  cd "${srcdir}/${pkgname}"
  make
}

package()
{
  cd "${srcdir}/${pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}


